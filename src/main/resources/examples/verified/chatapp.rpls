def sendMessage(from: UserId, content: String): MessageId {
    var m: MessageId
    atomic {
        m = new MessageId
        call message(NestedOp(m, author(Assign(from))))
        call message(NestedOp(m, content(Assign(content))))
        call chat(Add(m))
    }
    return m
}

def editMessage(id: MessageId, newContent: String) {
    atomic {
        if (message(KeyExists(id))) {
            call message(NestedOp(id, content(Assign(newContent))))
        }
    }
}

def deleteMessage(message_id: MessageId) {
    atomic {
        if (message(KeyExists(message_id))) {
            call chat(Remove(message_id))
            call message(DeleteKey(message_id))
        }
    }
}


def getMessage(m: MessageId): getMessageResult {
    atomic {
        if (message(KeyExists(m))) {
            return found(message(NestedQry(m, author(Read))), message(NestedQry(m, content(Read))))
        } else {
            return notFound()
        }
    }
}


type getMessageResult =
    notFound()
  | found(author: UserId, content: String)

type UserId
idtype MessageId
type String

crdt chat: Set_rw[MessageId]

crdt message: Map_dw[MessageId, {
    author: Register[UserId],
    content: MultiValueRegister[String]
}]

// integrity constraint:
//invariant forall m: MessageId ::
//    chat_contains(m) ==> message_exists(m)


// getMessage returns correct authors
invariant (forall g: InvocationId, m: MessageId, author: UserId, content: String ::
     g.info == getMessage(m)
  && g.result == getMessage_res(found(author, content))
  ==> (exists s: InvocationId, content2: String :: s.info == sendMessage(author, content2)))

// additional invariants:
// for every author-assignment there is a corresponding invocation of sendMessage
invariant forall c: CallId, m: MessageId, u: UserId ::
    c.op == message(NestedOp(m, author(Assign(u))))
    ==> (exists i: InvocationId, s: String ::
            i.info == sendMessage(u, s))

// if there is an assignment of the content field, there also is one for the author field that happened before:
invariant forall c1: CallId, m: MessageId, s: String ::
    c1.op == message(NestedOp(m, content(Assign(s))))
    ==> (exists c2: CallId, u: UserId ::
            c2.op == message(NestedOp(m, author(Assign(u))))
            && c2 happened before c1)


// there is no update after a delete
invariant !(exists write: CallId, delete: CallId, m: MessageId ::
       ((exists u: UserId ::  write.op == message(NestedOp(m, author(Assign(u)))))
        || (exists s: String ::  write.op == message(NestedOp(m, content(Assign(s))))))
    && delete.op == message(DeleteKey(m))
    && delete happened before write)
